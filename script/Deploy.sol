// SPDX-License-Identifier: LicenseRef-DCL-1.0
// SPDX-FileCopyrightText: Copyright (c) 2020 Rain Open Source Software Ltd
pragma solidity =0.8.25;

import {Script} from "forge-std-1.16.2/src/Script.sol";
import {LibDataContract} from "rain-datacontract-0.1.0/src/lib/LibDataContract.sol";
import {LibDecimalFloatDeploy} from "../src/lib/deploy/LibDecimalFloatDeploy.sol";
import {LibRainDeploy} from "rain-deploy-0.1.7/src/lib/LibRainDeploy.sol";
import {DecimalFloat} from "../src/concrete/DecimalFloat.sol";
import {LibEtchLogTables} from "./lib/LibEtchLogTables.sol";

/// @dev Hash of the "log-tables" deployment suite string. When the
/// `DEPLOYMENT_SUITE` env var is set to "log-tables", the script deploys the
/// log/antilog lookup tables as a data contract.
bytes32 constant DEPLOYMENT_SUITE_TABLES = keccak256("log-tables");

/// @dev Hash of the "decimal-float" deployment suite string. When the
/// `DEPLOYMENT_SUITE` env var is set to "decimal-float" (or omitted, as it is
/// the default), the script deploys the DecimalFloat contract.
bytes32 constant DEPLOYMENT_SUITE_CONTRACT = keccak256("decimal-float");

contract Deploy is Script {
    function run() external {
        uint256 deployerPrivateKey = vm.envUint("DEPLOYMENT_KEY");

        bytes32 suite = keccak256(bytes(vm.envOr("DEPLOYMENT_SUITE", string("decimal-float"))));
        if (suite == DEPLOYMENT_SUITE_TABLES) {
            LibRainDeploy.deployAndBroadcast(
                vm,
                LibRainDeploy.supportedNetworks(),
                deployerPrivateKey,
                LibDataContract.contractCreationCode(LibDecimalFloatDeploy.combinedTables()),
                "",
                LibDecimalFloatDeploy.ZOLTU_DEPLOYED_LOG_TABLES_ADDRESS,
                LibDecimalFloatDeploy.LOG_TABLES_DATA_CONTRACT_HASH,
                new address[](0)
            );
        } else if (suite == DEPLOYMENT_SUITE_CONTRACT) {
            // DecimalFloat's constructor calls
            // LibDecimalFloatDeploy.checkLogTablesDeployed(), which reads
            // extcodesize/codehash at ZOLTU_DEPLOYED_LOG_TABLES_ADDRESS.
            // The simulation pass runs before the broadcast pass, so if
            // log-tables hasn't actually been deployed yet on this chain
            // the simulation reverts. Plant the runtime bytecode locally
            // so simulation matches the post-broadcast on-chain state.
            if (LibDecimalFloatDeploy.ZOLTU_DEPLOYED_LOG_TABLES_ADDRESS.code.length == 0) {
                LibEtchLogTables.etchLogTables(vm);
            }

            address[] memory decimalFloatDependencies = new address[](1);
            decimalFloatDependencies[0] = LibDecimalFloatDeploy.ZOLTU_DEPLOYED_LOG_TABLES_ADDRESS;
            LibRainDeploy.deployAndBroadcast(
                vm,
                LibRainDeploy.supportedNetworks(),
                deployerPrivateKey,
                type(DecimalFloat).creationCode,
                "src/concrete/DecimalFloat.sol:DecimalFloat",
                LibDecimalFloatDeploy.ZOLTU_DEPLOYED_DECIMAL_FLOAT_ADDRESS,
                LibDecimalFloatDeploy.DECIMAL_FLOAT_CONTRACT_HASH,
                decimalFloatDependencies
            );
        } else {
            revert(
                "Invalid deployment suite specified. Please set the DEPLOYMENT_SUITE environment variable to either 'log-tables' or 'decimal-float'."
            );
        }
    }
}
